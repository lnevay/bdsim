! strength variables

! 'IT' = insertion triplet
QUAD.K1.IT.A =  0.002;
QUAD.K1.IT.B = -0.04;
QUAD.K1.IT.C :=  QUAD.K1.IT.A;
QUAD.K1.IT.D := -QUAD.K1.IT.C;
QUAD.K1.IT.E := -QUAD.K1.IT.B;
QUAD.K1.IT.F := -QUAD.K1.IT.A;

! 'IM' = insertion matching
! L0 is from start of insertion (left side) increasing towards IP
QUAD.K1.IM.L0 = 0.6*QUAD.K1L.D;
QUAD.K1.IM.L1 = 2*QUAD.K1L.F;
QUAD.K1.IM.L2 = 2*QUAD.K1L.F;
QUAD.K1.IM.L3 = QUAD.K1L.D;
QUAD.K1.IM.L4 = QUAD.K1L.F;
! R0 is from end of insertion (right side) increasing towards IP
QUAD.K1.IM.R0 := -QUAD.K1.IM.L0;
QUAD.K1.IM.R1 := -QUAD.K1.IM.L1;
QUAD.K1.IM.R2 := -QUAD.K1.IM.L2;
QUAD.K1.IM.R3 := -QUAD.K1.IM.L3;
QUAD.K1.IM.R4 := -QUAD.K1.IM.L4;

! Element definition
QUAD.IM.L0: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.L0, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};
QUAD.IM.L1: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.L1, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};
QUAD.IM.L2: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.L2, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};
QUAD.IM.L3: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.L3, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};
QUAD.IM.L4: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.L4, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};
QUAD.IM.R0: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.R0, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};
QUAD.IM.R1: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.R1, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};
QUAD.IM.R2: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.R2, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};
QUAD.IM.R3: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.R3, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};
QUAD.IM.R4: QUADRUPOLE, L := QUAD_LENGTH, K1 := QUAD.K1.IM.R4, APERTYPE=rectcircle, APERTURE={0.05, 0.03, 0.05, 0.00};

QUAD.IT.A: QUADRUPOLE, L := 0.8*IR_QUAD_LENGTH, K1:=QUAD.K1.IT.A, APERTYPE=circle, APERTURE={0.07, 0, 0, 0};
QUAD.IT.B: QUADRUPOLE, L := 1.3*IR_QUAD_LENGTH, K1:=QUAD.K1.IT.B, APERTYPE=circle, APERTURE={0.07, 0, 0, 0};
QUAD.IT.C: QUADRUPOLE, L := 0.8*IR_QUAD_LENGTH, K1:=QUAD.K1.IT.C, APERTYPE=circle, APERTURE={0.05, 0, 0, 0};
QUAD.IT.D: QUADRUPOLE, L := 0.8*IR_QUAD_LENGTH, K1:=QUAD.K1.IT.D, APERTYPE=circle, APERTURE={0.05, 0, 0, 0};
QUAD.IT.E: QUADRUPOLE, L := 1.3*IR_QUAD_LENGTH, K1:=QUAD.K1.IT.E, APERTYPE=circle, APERTURE={0.07, 0, 0, 0};
QUAD.IT.F: QUADRUPOLE, L := 0.8*IR_QUAD_LENGTH, K1:=QUAD.K1.IT.F, APERTYPE=circle, APERTURE={0.07, 0, 0, 0};

! Sequence definition
! low beta insertion -> 4 cells
REAL tripletmargin = 2.5;
lowbeta: sequence, refer=centre, l=L_IR;
lowbeta_start: marker, at=0.0;
QUAD.F.H,    at = 0.25 * QUAD_LENGTH;
QUAD.IM.L0,  at = L_CELLH;
QUAD.IM.L1,  at = L_CELL;
!QUAD.IM.L2,  at = L_CELL + L_CELLH;
!QUAD.IM.L3,  at = 2 * L_CELL;
!QUAD.IM.L4,  at = 2 * L_CELL;
lowbetatripletinput: marker, at = L_IRH - L_STAR   - (4*IR_QUAD_LENGTH) - (2*tripletmargin);
QUAD.IT.A,   at = L_IRH - L_STAR - IR_QUAD_LENGTHH - (2*IR_QUAD_LENGTH) - (2*tripletmargin);
QUAD.IT.B,   at = L_IRH - L_STAR - IR_QUAD_LENGTHH - IR_QUAD_LENGTH - tripletmargin;
QUAD.IT.C,   at = L_IRH - L_STAR - IR_QUAD_LENGTHH;
ip1: marker, at = L_IRH, APERTYPE=circle, APERTURE={0.01, 0, 0, 0};
QUAD.IT.D,   at = L_IRH + L_STAR + IR_QUAD_LENGTHH;
QUAD.IT.E,   at = L_IRH + L_STAR + IR_QUAD_LENGTHH + IR_QUAD_LENGTH + tripletmargin;
QUAD.IT.F,   at = L_IRH + L_STAR + IR_QUAD_LENGTHH + (2*IR_QUAD_LENGTH) + (2*tripletmargin);
lowbetatripletoutput: marker, at = L_IRH + L_STAR  + (4*IR_QUAD_LENGTH) + (2*tripletmargin);
!QUAD.IM.R4,  at = L_IR - (2 * L_CELL);
!QUAD.IM.R3,  at = L_IR - (2 * L_CELL);
!QUAD.IM.R2,  at = L_IR - L_CELL - L_CELLH;
QUAD.IM.R1,  at = L_IR - L_CELL;
QUAD.IM.R0,  at = L_IR - L_CELLH;
QUAD.D.H,    at = L_IR - (0.25 * QUAD_LENGTH);
lowbeta_end: marker, at=L_IR;
endsequence;

! Matching
use, sequence=lowbeta;
match, sequence=lowbeta, BETX=fodo_end->BETX, BETY=fodo_end->BETY, alfx=fodo_end->alfx, alfy=fodo_end->alfy;
vary, name=QUAD.K1.IM.L0, step=0.00001, upper=-0.00001;
vary, name=QUAD.K1.IM.L1, step=0.00001, lower= 0.00001;
!vary, name=QUAD.K1.IM.L2, step=0.00001;!, upper=-0.00001;
vary, name=QUAD.K1.IT.A,  step=0.00001, lower=0.001;
vary, name=QUAD.K1.IT.B,  step=0.00001, upper=-0.001;
!constraint, sequence=lowbeta, range=QUAD.IM.L1, betx<200;
!constraint, sequence=lowbeta, range=QUAD.IM.L1, bety<200;
!constraint, sequence=lowbeta, range=QUAD.IT.A, betx>150;
!constraint, sequence=lowbeta, range=QUAD.IT.A, bety>150;
!constraint, sequence=lowbeta, range=QUAD.IT.B, betx>300;
!constraint, sequence=lowbeta, range=QUAD.IT.B, bety>300;
constraint, sequence=lowbeta, range=ip1, BETX=2.5, ALFX=0.0, ALFY=0.0, BETY=2.5;
constraint, sequence=lowbeta, range=lowbeta_end, BETX=fodo_start->BETY, BETY=fodo_start->BETX, alfx=fodo_start->alfy, alfy=fodo_start->alfx, mux=8*phaseadvance, muy=8*phaseadvance;
lmdif, calls=3000, tolerance=1.0e-16;
endmatch;

twiss, save, sequence=lowbeta, beta0=fodo_end, file=lowbetainsertion.tfs;